// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    //// handle zx, nx
    Mux16(a=x, b=false, sel=zx, out=zxtmp);
    Not16(in=zxtmp, out=nxt);
    Mux16(a=zxtmp, b=nxt, sel=nx, out=nxtmp);

    //// handle zy, ny
    Mux16(a=y, b=false, sel=zy, out=zytmp);
    Not16(in=zytmp, out=nyt);
    Mux16(a=zytmp, b=nyt, sel=ny, out=nytmp);

    //// handle f
    Add16(a=nxtmp, b=nytmp, out=xaddy);
    And16(a=nxtmp, b=nytmp, out=xandy);
    Mux16(a=xandy, b=xaddy, sel=f, out=ftmp);

    //// handle no
    Not16(in=ftmp, out=nftmp);
    /// tricky XD
    Mux16(a=ftmp, b=nftmp, sel=no, out=out, out[0..7]=out1, out[8..15]=out2, out[15]=o15);

    //// handle zr, ng
    Or8Way(in=out1, out=tz1);
    Or8Way(in=out2, out=tz2);
    Or(a=tz1, b=tz2, out=tz);
    Mux(a=true, b=false, sel=tz, out=zr);
    Mux(a=false, b=true, sel=o15, out=ng);

}